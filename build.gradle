plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group = 'dev.eymen'
version = '2.0.2-STABLE'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven { url 'https://jitpack.io' }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'com.github.oraxen:oraxen:1.157.5'
    compileOnly("io.github.miniplaceholders:miniplaceholders-api:2.2.0")
    implementation "dev.dejvokep:boosted-yaml:1.3.1"
    implementation 'org.json:json:20230618'
    implementation "dev.jorel:commandapi-bukkit-shade:9.0.3"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'dev.jorel.commandapi', 'dev.eymen.shaded.commandapi'
    relocate 'dev.dejvokep.boostedyaml', 'dev.eymen.shaded.boostedyaml'
    relocate "org.json", "dev.eymen.shaded.json"
    exclude "org/jetbrains/annotations/**"
    exclude "org/intellij/lang/**"
}